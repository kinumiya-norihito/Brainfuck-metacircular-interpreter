【入力部】
-
{入力が00(改行)ならループ終了}
[
    {調整用加算}
    +
    {入力部まで移動}
    >>>>
    {入力}
    ,
    ----------
    {終了命令}
    [<]-[>]<<+[->+<<+>]>
    ---------------------------------
    {加算命令}
    [<]-[>]<<+[->+<<+++++++>]>
    -
    {入力命令}
    [<]-[>]<<+[->+<<+++++++++>]>
    -
    {減算命令}
    [<]-[>]<<+[->+<<++++++>]>
    -
    {出力命令}
    [<]-[>]<<+[->+<<++++++++>]>
    --------------
    {ポインタ減算命令}
    [<]-[>]<<+[->+<<++++>]>
    --
    {ポインタ加算命令}
    [<]-[>]<<+[->+<<+++++>]>
    -----------------------------
    {ループ開始命令}
    [<]-[>]<<+[->+<<+++>]>
    --
    {ループ終了命令}
    [<]-[>]<<+[->+<<++>]>
    {入力文字00化}
    [+]
    {入力命令参照}
    <<
    {命令が不正な場合再入力できる位置へ戻る}
    [<]-[>]<<+[->+<<<]>
    {終了命令でループを抜ける処理}
    -
]
【準備部】
{テープ部の先頭付近へ移動}
>>
{テープ部の始点となる値を入力}
(これは必ずテープが|01|XX|00|となるようにするため、最初が|00|XX|00|だったらダメ)
+
{コード部の末尾に移動}
<<<<
{余分な数字を調整する}
[-<<]
{コード部の先頭に移動}
>>
【実行部】
{命令が00(終了)ならループ終了}
[
    -
    {ループ終了命令}
    [<]-[>]<<+[
		->+
		{命令の復元}
		(この直後に移動するので)
		+
		{ネストを数える紐}
		(最初のとっかかり)
		<<<+
		{ネスト数が00ならループ終了}
		[
			{ネスト数を表すフラグを退避}
			-
			{命令をセレクト}
			>
			{命令確認}
			-
			{ネスト数を増やす}
			[<]-[>]<<+[->+<<<[<<]+[>>]]>
			-
			{ネスト数を減らす}
			(退避しているのであとで辻褄が合うように違う場所にフラグを置く)
			[<]-[>]<<+[->+>>>+<<<<]>
			{命令の復元}
			++
			{辻褄}
			<+
			{ネスト数を減らすフラグに応じてネスト数を減らす}
			>>>>[-<<<<[<<]>>->>[>>]>>]<<<<
			{ネスト数に関するフラグを移動させる}
			[-<<[<<]+[>>]]
			<<
		]
		>>>
		{命令を01減算}
		(辻褄を合わせるため)
		-
		{入力位置に対応する位置に調整}
		<
	]>
    -
    {ループ開始命令}
    [<]-[>]<<+[
		->+
		{テープに移動}
		>>[>>]>>[>>]<<
		{テープのポインタ位置を表す01を00にする}
		-
		>
		[<]-[>]<<+
		[
			{命令位置に移動}
			[<<]<<[<<]
			{命令復元}
			(この直後に移動するので)
			++
			{ネストを数える紐}
			(最初のとっかかり)
			>>>+
			{ネスト数が00ならループ終了}
			[
				{ネスト数を表すフラグを退避}
				-
				{命令をセレクト}
				<
				{命令確認}
				-
				{ネスト数を減らす}
				[<]-[>]<<+[->+<<<+>>]>
				-
				{ネスト数を増やす}
				[<]-[>]<<+[->+>>>[>>]+[<<]<<]>
				{命令の復元}
				++
				{辻褄}
				>+
				{ネスト数を減らすフラグに応じてネスト数を減らす}
				<<<<[->>>>[>>]<<-<<[<<]<<]>>>>
				{ネスト数に関するフラグを移動させる}
				[->>[>>]+[<<]]
				>>
			]
			<<<
			{ジャンプが行われたことを示すフラグ}
			<+
			{命令を00にする}
			(帰還用)
			>-
			{テープに移動}
			>>[>>]>>[>>]
			{テープを初期状態に戻す}
			<+<-
		]
		{テープ位置を表すフラグを復元}
		+
		{命令位置に移動}
		[<<]<<[<<]<
		{ジャンプが行われてた場合、命令がループ終了文字を指し示しているので調整}
		[->-<]
	]>
	-
    {ポインタ減算命令}
    [<]-[>]<<+[->+>>[>>]>>[>>]<<-<<[<<]<<[<<]<]>
    -
    {ポインタ加算命令}
    [<]-[>]<<+[->+>>[>>]>>[>>]+[<<]<<[<<]<]>
	-
    {減算命令}
    [<]-[>]<<+[->+>>[>>]>>[>>]<-<[<<]<<[<<]<]>
    -
    {加算命令}
    [<]-[>]<<+[->+>>[>>]>>[>>]<+<[<<]<<[<<]<]>
    -
    {出力命令}
    [<]-[>]<<+[->+>>[>>]>>[>>]<.<[<<]<<[<<]<]>
    -
    {入力命令}
    [<]-[>]<<+[->+>>[>>]>>[>>]<,<[<<]<<[<<]<]>
    {減算してた命令の復元}
    ++++++++
    {次の命令へ移動}
    >>
]
